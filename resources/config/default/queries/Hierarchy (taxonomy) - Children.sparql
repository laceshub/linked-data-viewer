PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

#########################
# This query returns the taxonomy children to a parent term.
#
# INPUT:
#   ?term_iri: The IRI of the parent term
#########################

SELECT ?entry_iri ?entry_text ?entry_icon (?fakeChildCount AS ?entry_childCount)
WHERE {
  # find classes
  ?class wdt:P279 ?term_iri .
  
  # get name of class
  ?class rdfs:label ?classLabel .
  FILTER (LANG(?classLabel) = "en")
  
  # do subclasses exist?
  OPTIONAL {
    FILTER EXISTS {
      ?subClass wdt:P279 ?class .
    }
    BIND (1 AS ?fakeChildCount) .
  }

  BIND (?class AS ?entry_iri) .
  BIND (?classLabel AS ?entry_text) .
  BIND ("icon-term-type" AS ?entry_icon) .
}
ORDER BY ?entry_text ?entry_iri