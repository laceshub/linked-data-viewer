PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

###################################
# This query presents geometric details of the selected item
#
# INPUT:
#   ?term_iri: the selected item (a class or an individual)
# OUTPUT:
#   ?entry_iri:      an individual object (?term_iri or an instance thereof)
#   ?entry_text:     the name of the individual object, shown in popup in the 
#                    case ?location_text is undefined
#   ?location_wkt:   the location as Well Known Text representation
#   ?location_crs:   a URI of the Coordinate Reference System (CRS) to be used
#                    in case it has not been explicitly specified as part of 
#                    ?location_wkt and is known to deviate from WGS 84
#   ?location_text:  literal with string or HTML content to be shown in the 
#                    popup for the object
#   ?location_layer: identifier for the layer to be used (optional)
#   ?location_color: the color, specified as rgb or hsla;
#                    the latter can also be defined instead using vars
#                    ?location_hue, ?location_saturation, ?location_lightness, 
#                    and ?location_opacity
#          ( see https://docs.mapbox.com/mapbox-gl-js/style-spec/types/#color )
#   ?location_thickness: the thickness of the line
###################################


SELECT ?entry_iri ?entry_text ?location_wkt ?location_crs ?location_text ?location_color
WHERE {
  {
    ?entry_iri wdt:P31/wdt:P279* ?term_iri .
    ?entry_iri wdt:P625 ?location_wkt .
  } UNION {
    BIND (?term_iri AS ?entry_iri) .
    ?entry_iri wdt:P625 ?location_wkt .
  }
  OPTIONAL {
    ?entry_iri rdfs:label ?entry_text .
    FILTER (lang(?entry_text) = "en") .
  }
  BIND ("#00aa00" AS ?location_color) .
  BIND (STRDT(CONCAT("<h1>", COALESCE(?entry_text, "Object"), "</h1>IRI: ", STR(?entry_iri)), rdf:HTML) AS ?location_text) .
}
LIMIT 1000
